\chapter{Coq Definitions}

For posterity, we use this section to define and discuss select Coq definitions
in the implementation and formalization of the verified compiler. We attempt to
convey what worked well, and what posed significant challenges in the hope of
informing future work in this area. The formalization is available at
\url{https://github.com/stelleg/cem\_coq} and as of this dissertation has been
successfully type-checked with Coq version 8.9. The version used for this
dissertation is tagged with \texttt{dissertation}, which at the time of writing
enables it to be downloaded as a GitHub release tarball.

\section{Instruction Machine} 

We start by defining and describing the assembly language of the abstract
instruction machine, the target of the verified compiler. This is the
formalization of language defined in Figure~\ref{fig:im_syntax}. Note the use of
coercions to help ease the use of type-safe read and write operands, and an
infix operator to duplicate common syntax for real world assembly languages when
indexing an offset. Note that for our purposes, we only ever need a constant
offset, but one could easily extend the language to allow for offsets to be
defined by a read operand.  

\begin{verbatim}
Definition Word := nat.
Definition Ptr := nat.

Inductive Reg := 
  | IP
  | EP
  | R1
  | R2.

Inductive WO := 
  | WR : Reg -> WO
  | WM : Reg -> nat -> WO.

Coercion WR : Reg >-> WO.
Infix "%" := WM (at level 30).

Inductive RO := 
  | RW : WO -> RO
  | RC : nat -> RO.

Coercion RW : WO >-> RO.
Coercion RC : nat >-> RO.

Inductive Instr : Type :=
  | push : RO -> Instr
  | pop : WO -> Instr
  | new : nat -> WO -> Instr 
  | mov : RO -> WO -> Instr.

Inductive BasicBlock : Type :=
  | instr : Instr -> BasicBlock -> BasicBlock
  | jump : option (RO*Ptr) -> RO -> BasicBlock.

Definition Program := list BasicBlock.
\end{verbatim}

Finally, note that we use a list of basic blocks as our program. This list is
indexed by integers in the machine semantics, so translating this to a Harvard 
architecture machine semantics should be relatively straightforward. 

Now that we have the language for our abstract instruction machine target, we
can look at the formal semantics for it. Without loss of generality, we choose a
step relation on instructions, wrapped by a step instruction on full basic
blocks. This eases reasoning about the relation to the small step \ce
relation. This is a formalization of the semantics described in
Figure~\ref{fig:im_semantics}. 

We start with a our register file and machine state, along with a
straightforward semantics for reading and writing from operands given a 
machine state. We omit some uninteresting helper functions. 

\begin{verbatim}
Inductive RegisterFile := mkrf {
  ip : Ptr;
  ep : Ptr;
  r1 : Ptr; 
  r2 : Ptr
}. 

Inductive State := st {
  st_rf : RegisterFile;
  st_p : Program;
  st_s : Stack;
  st_h  : Heap
}.

Open Scope nat_scope. 
Inductive read : RO -> State -> Ptr -> Type :=
  | read_reg : forall r s, read (RW (WR r)) s (rff (st_rf s) r)
  | read_mem : forall r o rf p s h v, 
    lookup (o+rff rf r) h = Some v ->
    read (RW (WM r o)) (st rf p s h) v
  | read_const : forall c s, read (RC c) s c.

Inductive write : WO -> Word -> State -> State -> Type :=
  | write_reg : forall r rf p s h w, 
    write (WR r) w (st rf p s h) (st (upd r w rf) p s h)
  | write_mem : forall r o rf p s h w, 
    write (WM r o) w (st rf p s h) 
                     (st rf p s (replace beq_nat (o+rff rf r) w h)).
\end{verbatim}

Given our machine definitions and read and write semantics, we can move directly
to defining the step relation for instructions. The \texttt{step\_bb} relation
defines the execution of a full basic block by induction on the instructions
contained in that basic blocks. The \texttt{step} relation then wraps that
relation and defines how the state changes given execution of the basic block
pointed to by the instruction pointer. Note that we don't define an explicit
halting relation. Instead, the machine will be stuck when the current code to be
executed is a value and the stack is empty, which is where the small step
\ce semantics also get stuck. 

\begin{verbatim}
Inductive step_bb : BasicBlock -> State -> State -> Type := 
  | step_push : forall rf p s h ro is v sn,
  read ro (st rf p s h) v -> 
  step_bb is (st rf p (v::s) h) sn -> 
  step_bb (instr (push ro) is) (st rf p s h) sn
  | step_pop : forall rf p s h wo is w s' sn,
  write wo w (st rf p s h) s' -> 
  step_bb is s' sn ->
  step_bb (instr (pop wo) is) (st rf p (w::s) h) sn
  | step_new : forall rf p s h wo is w s' n sn,
  (forall i, i < n -> not (In (i+w) (domain h))) ->
  w > 0 ->
  write wo w (st rf p s (zeroes n w ++ h)) s' ->
  step_bb is s' sn -> 
  step_bb (instr (new n wo) is) (st rf p s h) sn 
  | step_mov : forall s is ro wo s' v sn, 
  read ro s v -> write wo v s s' -> 
  step_bb is s' sn -> 
  step_bb (instr (mov ro wo) is) s sn
  | step_jump0 : forall ro k j s s', 
  read ro s 0 ->
  write (WR IP) k s s' -> 
  step_bb (jump (Some (ro, k)) j) s s'
  | step_jumpS : forall ro k j s s' l k', 
  l > 0 ->
  read ro s l ->
  read j s k -> 
  write (WR IP) k s s' -> 
  step_bb (jump (Some (ro, k')) j) s s'
  | step_jump : forall ro s s' l, 
  read ro s l ->
  write (WR IP) l s s' -> 
  step_bb (jump None ro) s s'
.

Inductive step : transition State :=
  | enter : forall rf p s h k bb sn,
    read IP (st rf p s h) k -> 
    nth_error p k = Some bb -> 
    step_bb bb (st rf p s h) sn ->
    step (st rf p s h) sn.
\end{verbatim}

This allows 
